### START !_#_!
# omo-index - версия под полуавтоматическую обротку для скрипта ./momo.sh
# производит предварительную обработку текста до передачи его скрипту awk.
# Последняя версия файла находится тут: https://github.com/Balamoote/gtts-scripts

s=\.\.\.=…=gI
#s=(\xcc\x81)+=\xcc\x81=g

# Чистка пробелов
s=(\S)\s+=\1 =g
s=(<p>)\s+=\1=g
s=\s+(</p>)=\1=g

#--------------------------------------------------------------------------------------------------------------------------------------------------------
# Размеры
s=([0-9])(\s?)([xх])(\s?)([0-9])=\1\2\3\xcc\xa0\4\5=gI
s=([0-9])(\s?)([xх])(\s?)([0-9])=\1\2\3\xcc\xa0\4\5=gI
s=([0-9])(\s?)([xх])(\s?)([0-9])=\1\2\3\xcc\xa0\4\5=gI

# Год
s=(в)\s([0-9] г)\.=\1 \2\xe2\x80\xa4\xcc\xa0=gI

# Замена точки на ․ U+2024 для раздельного произношения инициалов в TTSLexx
s=\b([ЁАБВГДЕЖЗИЙКЛМНОПРСТУФХЦЧШЩЪЫЬЭЮЯ])\.(\s?[ЁАБВГДЕЖЗИЙКЛМНОПРСТУФХЦЧШЩЪЫЬЭЮЯ])\.(\s?[ЁАБВГДЕЖЗИЙКЛМНОПРСТУФХЦЧШЩЪЫЬЭЮЯ])\B=\1\xe2\x80\xa4\2\xe2\x80\xa4\3=g
s=\b([ЁАБВГДЕЖЗИЙКЛМНОПРСТУФХЦЧШЩЪЫЬЭЮЯ])\.(\s?[ЁАБВГДЕЖЗИЙКЛМНОПРСТУФХЦЧШЩЪЫЬЭЮЯ])\B=\1\xe2\x80\xa4\2=g

s=(\B[абвгдежзийклмнопрстуфхцчшщъыьэюяё])\s([ЁАБВГДЕЖЗИЙКЛМНОПРСТУФХЦЧШЩЪЫЬЭЮЯ])\.(\s?[ЁАБВГДЕЖЗИЙКЛМНОПРСТУФХЦЧШЩЪЫЬЭЮЯ])\.=\1 \2\xe2\x80\xa4\3\xe2\x80\xa4=g
s=(\B[абвгдежзийклмнопрстуфхцчшщъыьэюяё]\xcc\x81?)\s(\s?[ЁАБВГДЕЖЗИЙКЛМНОПРСТУФХЦЧШЩЪЫЬЭЮЯ])\.=\1 \2\xe2\x80\xa4=g

# добавление метки ‧ U+2027 для отдельных букв
s=([-0-9])([ЁАБВГДЕЖЗИЙКЛМНОПРСТУФХЦЧШЩЪЫЬЭЮЯабвгдежзийклмнопрстуфхцчшщъыьэюяё])\b([^-\xcc\x81\xcc\xa0\xcc\xa3\xcc\xa4\xcc\xad\xcc\xb0’])=\1\2\xe2\x80\xa7\3=g
s=([^АБВГДЕЁЖЗИЙКЛМНОПРСТУФХЦЧШЩЪЫЬЭЮЯабвгдеёжзийклмнопрстуфхцчшщъыьэюя\xcc\x81\xcc\xa0\xcc\xa3\xcc\xa4\xcc\xad\xcc\xb0][ЁАБВГДЕЖЗИЙКЛМНОПРСТУФХЦЧШЩЪЫЬЭЮЯабвгдежзийклмнопрстуфхцчшщъыьэюяё])([-0-9)"»’>]”][^АБВГДЕЁЖЗИЙКЛМНОПРСТУФХЦЧШЩЪЫЬЭЮЯабвгдеёжзийклмнопрстуфхцчшщъыьэюя\xcc\x81\xcc\xa0\xcc\xa3\xcc\xa4\xcc\xad\xcc\xb0])=\1\xe2\x80\xa7\2=g
s=([^АБВГДЕЁЖЗИЙКЛМНОПРСТУФХЦЧШЩЪЫЬЭЮЯабвгдеёжзийклмнопрстуфхцчшщъыьэюя\xcc\x81\xcc\xa0\xcc\xa3\xcc\xa4\xcc\xad\xcc\xb0][-0-9])([ЁАБВГДЕЖЗИЙКЛМНОПРСТУФХЦЧШЩЪЫЬЭЮЯабвгдежзийклмнопрстуфхцчшщъыьэюяё])([-0-9])=\1\2\xe2\x80\xa7\3=g
s=([(])([ЁАБВГДЕЖЗИЙКЛМНОПРСТУФХЦЧШЩЪЫЬЭЮЯабвгдежзийклмнопрстуфхцчшщъыьэюяё])([)])=\1\2\xe2\x80\xa7\3=g
s=([^-\xcc\x81\xcc\xa0\xcc\xa3\xcc\xa4\xcc\xad\xcc\xb0])\b([АБВИКСабвикс])([)"“’”»])=\1\2\xe2\x80\xa7\3=g

# Сокращения, предотвращение "конца предложения после сокращений", ․ U+2024, как точка и комбинация её с ̠  U+0320
s=\sн\.(\s?)э\.= н\xe2\x80\xa4\1э\xe2\x80\xa4=gI
s=S\.T\.A\.L\.K\.E\.R\.=S\x20\x24T\x20\x24A\x20\x24L\x20\x24K\x20\x24E\x20\x24R\x20\x24=gI
s=\b(п|пп|ч|ст)\.(\s?[0-9]|\s?[ICLVXM])=\1\xcc\xa0\xe2\x80\xa4\2=gI
s=\b(гл|пер|перев|см|ср)\.\s=\1\xcc\xa0\xe2\x80\xa4 =gI

# Удалить повторы служебных
s=(\xcc\x81)+=\1=g
s=(\xcc\xa0)+=\1=g
s=(\xcc\xa3)+=\1=g
s=(\xcc\xa4)+=\1=g
s=(\xcc\xad)+=\1=g
s=(\xcc\xb0)+=\1=g
s=(\xe2\x80\xa4)+=\1=g
s=(\xe2\x80\xa7)+=\1=g

#::end::

### #LOOP#TEST# !_#_!
# Раскомментировать строку ниже для "пастеризации" текста для выявления ошибок.

#s=^.*$=\L\0=g; s=ё=е=g; s=[\xcc\x81\xcc\xa0\xcc\xa3\xcc\xa4\xcc\xad\xcc\xb0\xe2\x80\xa4]+==g; s=\t+= =g

# Справка по специальным символам
# Unicode   UTF-8,sed       TTSLexx     Char    Example   Заглушка	Комментарий
#
# U+0301    \xcc\x81        \u0301      ́        а́         X1Ъ и ЪЪЪ	знак ударения (в поисковой части regex в lexx)
# U+0320    \xcc\xa0        \u0320      ̠        а̠         X0Ъ		комбинирующая чёрточка снизу, как метка различных замен ("е" на "э", "я" на "йа", "о" на "а" и т.д.)
# U+0323    \xcc\xa3        \u0323      ̣        а̣         X3Ъ		комбинирующая точка снизу, изображает "ь", также используется для вывода слова из-под обработки lexx'ом
# U+0324    \xcc\xa4        \u0324      ̤        а̤         X4Ъ		комбинирующие две точки снизу, изображает "ъ", также используется для вывода слова из-под обработки lexx'ом
# U+032D    \xcc\xad        \u032d      ̭        a̭         X5Ъ		комбинирующий циркумфлекс снизу, как метка замены "я" на "ья"
# U+0330    \xcc\xb0        \u0330      ̰        а̰         X6Ъ		комбинирующая тильда снизу, изображает "й", замена "я" на "йа"
# U+2024    \xe2\x80\xa4    \u2024      ․       а․        X8Ъ		визуальная замена точки, чтобы избежать паузы в сокращениях, часть комбинаций
# U+2027    \xe2\x80\xa7    \u2027      ‧       а‧        X7Ъ		метка паузы
#
# Для grep формат следующий: "$'\xcc\x81'" . Вся последовательности, кодов заключаются в кавычки. Для совместимости в переменные лучше вводить через printf

